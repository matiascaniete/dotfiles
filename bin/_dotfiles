#compdef _dotfiles

_dotfiles() {
    local line
    _subcommands() {
        local -a subcommands
        subcommands=(
            "config:Config module"
            "dependencies:Dependencies module"
            "fonts:Fonts module"
            "init:Initialize dotfiles"
            "self-update:Update dotfiles"
        )

        _describe 'dotfiles' subcommands
    }

    _arguments -C \
        "1: :_subcommands" \
        "*:: :->args"

    _dotfiles_"${line[1]}" 2> /dev/null
}

_dotfiles_fonts() {
    _arguments -C \
        "1: :(install)"
}

_dotfiles_dependencies() {
    local line
    _arguments -C \
        "1: :(list install install-all)" \
        "*:: :->args"

    _dotfiles_dependencies_"${line[1]}" 2> /dev/null
}

_dotfiles_dependencies_install() {
    _values 'dependencies' $(dotfiles dependencies list)
}

_dotfiles_config() {
    local line
    _subcommands() {
        local -a subcommands
        subcommands=(
            "list:List configs"
            "install:Install configs"
            "install-all:Install all configs"
            "adopt:Adopt config from ~/.config"
            "edit:Edit config"
        )

        _describe 'config' subcommands
    }

    _arguments -C \
        "1: :_subcommands" \
        "*:: :->args"

    _dotfiles_config_"${line[1]}" 2> /dev/null
}

_dotfiles_config_install() {
    _values 'config to install' $(dotfiles config list)
}

_dotfiles_config_adopt() {
    _values 'config to adopt' $(dotfiles config adopt-list)
}

_dotfiles_config_edit() {
    _values 'config to edit' $(dotfiles config list)
}

compdef _dotfiles dotfiles

# vim:ft=bash
