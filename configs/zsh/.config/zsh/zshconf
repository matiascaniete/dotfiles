#!/usr/bin/env bash
# shellcheck disable=1090,1091

declare -A CONFIG_FILES
CONFIG_FILES[zshenv]=~/.zshenv
CONFIG_FILES[zshrc]="$ZDOTDIR"/.zshrc
CONFIG_FILES[exports]=$ZDOTDIR/exports
CONFIG_FILES[zshconf]=$ZDOTDIR/zshconf
CONFIG_FILES[keybind]=$ZDOTDIR/keybind
CONFIG_FILES[aliases]=$ZDOTDIR/aliases

# Aliases
source ~/.config/zsh/aliases

# Keybinds
source ~/.config/zsh/keybind

# FZF default key-bindings and completions
source "$(dirname "$FZF_BASE")"/../shell/key-bindings.zsh
source "$(dirname "$FZF_BASE")"/../shell/completion.zsh

# Zsh plugins
source ~/.local/share/zsh-plugins/zsh-history-substring-search/zsh-history-substring-search.zsh
source ~/.local/share/zsh-plugins/zsh-autosuggestions/zsh-autosuggestions.zsh
source ~/.local/share/zsh-plugins/fzf-tab/fzf-tab.plugin.zsh
source ~/.local/share/zsh-plugins/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh
source ~/.local/share/zsh-plugins/zsh-z/zsh-z.plugin.zsh

if [[ $DISABLE_ZSH_PLUGIN_NOTIFY != 1 ]]; then
    source ~/.local/share/zsh-plugins/zsh-notify/notify.plugin.zsh
fi

# shellcheck disable=2206
fpath=(~/.local/share/zsh-plugins/zsh-completions/src $fpath)
# shellcheck disable=2206,2128
fpath=($ZDOTDIR/custom-completions $fpath)

# ------ zsh custom functions ------

# reload and zconf functions
reload() {
    if [[ -f "$ZDOTDIR/.zcompdump" ]]; then
        rm "$ZDOTDIR"/.zcompdump && echo "zcompdump removed."
    fi

    if [[ -f "$XDG_CACHE_HOME/zsh/.zcompcache" ]]; then
        rm "$XDG_CACHE_HOME/zsh/.zcompcache" && echo "zcompcache removed."
    fi

    exec zsh
}

zconf() {
    file_key=${1:-zshrc}
    $EDITOR "${CONFIG_FILES[$file_key]}"
}

timezsh() {
    shell=${1-$SHELL}
    for i in $(seq 1 5); do
        printf "run #%s\t" "$i"
        /usr/bin/time -f "%E real\t%U user\t%S sys" "$shell" -i -c exit
    done
}

buffer2gpt-generate() {
    input=$BUFFER
    newcommand="gpt --model gpt-4o-mini 'Write a zsh command that does the following: \"$input\". Return the result as a single string as plain text, no formatting, ready to send directly into the terminal, with no comments, and no explanations, no markdown'"
    fc -R =(printf '%s # %s\n' "$newcommand" "$input" )
    # zle -R "" "Gpt command: $newcommand"

    zle -R "GPTing (generating) ..."
    output=$(eval "$newcommand")
    zle -R ""

    BUFFER=$output
    CURSOR=$#BUFFER
}

buffer2gpt-query() {
    input=$BUFFER
    newcommand="gpt --model gpt-4o-mini 'Explain in few words the following zsh command: \"$input\"'"
    zle -R "GPTing (asking) ..."
    output=$(eval "$newcommand")
    zle -R ""
    zle -M "$output"
}

buffer2gpt-menu() {
    {
        echo "buffer2gpt-generate|GPTing (generating)"
        echo "buffer2gpt-query|GPTing (asking)"
        } | fzf --delimiter='|' | cut -d '|' -f 1 | while read -r cmd; do
        zle reset-prompt
        $cmd
    done
}

autoload -z edit-command-line
zle -N edit-command-line
bindkey '^g^e' edit-command-line

zle -N buffer2gpt-generate
zle -N buffer2gpt-query
zle -N buffer2gpt-menu
zle -N reload
# bindkey '^g' buffer2gpt-generate
bindkey '^g' buffer2gpt-menu

rmenu-exit-zsh() {
    rmenu-exit
}

zle -N rmenu-exit-zsh
# bindkey meta+o 
bindkey '^[o' rmenu-exit-zsh

# zsh autocompletion helpers functions

__zshac_further_execute() {
    if [ ! -z "${line[1]}" ]; then
        if command -v "$1_${line[1]}" &>/dev/null; then
            "$1_${line[1]}" 2>/dev/null
        fi
    fi
}

__zshac_generic_subcommands() {
    nextcommand="$1"
    shift
    subcommands=("$@")

    _subcommands() {
        _describe "ohm $nextcommand" subcommands
    }

    _arguments -C \
        "1: :_subcommands" \
        "*::arg:->args"

    __zshac_further_execute "$nextcommand"
}

# ------ zsh options -------

# history
# You can make zsh add a bit more data (timestamp in unix epoch time and elapsed time of the command) by setting the EXTENDED_HISTORY shell option.
setopt EXTENDED_HISTORY
# share history across multiple zsh sessions
# setopt SHARE_HISTORY
setopt sharehistory
# append to history
# setopt APPEND_HISTORY
setopt appendhistory
# adds commands as they are typed, not at shell exit
setopt INC_APPEND_HISTORY
#ignore duplicates when searching
setopt HIST_FIND_NO_DUPS
# removes blank lines from history
setopt HIST_REDUCE_BLANKS

setopt hist_ignore_all_dups
setopt hist_ignore_space
setopt hist_save_no_dups
setopt hist_ignore_dups    ## Options section

# from garuda defaults .zshrc
setopt correct                                                  # Auto correct mistakes
setopt extendedglob                                             # Extended globbing. Allows using regular expressions with *
setopt nocaseglob                                               # Case insensitive globbing
setopt rcexpandparam                                            # Array expension with parameters
setopt nocheckjobs                                              # Don't warn about running processes when exiting
setopt numericglobsort                                          # Sort filenames numerically when it makes sense
setopt nobeep                                                   # No beep
setopt appendhistory                                            # Immediately append history instead of overwriting
setopt histignorealldups                                        # If a new command is a duplicate, remove the older one
setopt autocd                                                   # if only directory path is entered, cd there.
setopt auto_pushd
setopt pushd_ignore_dups
setopt pushdminus

# ------ zstyle -------

# _complete - This is the main completer we need to use for our completion.
# _approximate - This one is similar to _complete, except that it will try to correct what you’ve typed already (the context) if no match is found.
# _expand_alias - Expand an alias you’ve typed. It needs to be declared before _complete.
# _extensions - Complete the glob *. with the possible file extensions.
# zstyle ':completion:*' completer _expand_alias _extensions _complete
# zstyle ':completion:*' completer _expand_alias _extensions _complete _approximate

zstyle ':completion:*' verbose yes

# For the command cd, chdir and pushd, you can use a hyphen - for completing for an option
zstyle ':completion:*' complete-options true
        
zstyle ':completion:*' rehash true                              # automatically find new executables in path
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"         # Colored completion (different colors for dirs/files/etc)
zstyle ':completion:*' completer _expand _complete _ignored _approximate
zstyle ':completion:*' menu select
zstyle ':completion:*' select-prompt '%SScrolling active: current selection at %p%s'

# disable sort when completing `git checkout`
zstyle ':completion:*:git-checkout:*' sort false

# set descriptions format to enable group support
zstyle ':completion:*:descriptions' format '[%d]'
# zstyle ':completion:*:descriptions' format '%U%F{cyan}%d%f%u'

# switch group using `,` and `.`
zstyle ':fzf-tab:*' switch-group ',' '.'
zstyle ':fzf-tab:*' show-group full

# this is an example
zstyle ':fzf-tab:complete:(\\|)run-help:*' fzf-preview 'run-help $word'
zstyle ':fzf-tab:complete:(\\|*/|)man:*' fzf-preview 'man $word'

# preview directory's content with exa when completing cd
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'ls -1 --color=always $realpath' # remember to use single quote here!!!
# zstyle ':fzf-tab:complete:*:*' fzf-preview 'bat --color=always ${(Q)realpath}'

# Caching the Completion
# Using a cache for the completion can speed up some commands, like apt for example. Let’s add the following in our file to enable it:
# Speed up completions
zstyle ':completion:*' accept-exact '*(N)'
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path "$XDG_CACHE_HOME/zsh/.zcompcache"

# Case insensitive completion
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'

# Bash completion init
# autoload -U bashcompinit && bashcompinit

# Zsh completion init (IMPORTANT: this must go after fpath updates)
autoload -Uz compinit
compinit
# compinit -D

# vi: ft=sh
